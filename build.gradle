group "com.asmirnov"
version "1.0-SNAPSHOT"

apply plugin: "java"
apply plugin: 'gradle-one-jar'
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

mainClassName = "com.asmirnov.usrservice.UsrServiceApp"

project.ext {
    dropwizardVersion = "1.1.3"
}

// Add Gradle OneJar Plugin, see https://github.com/rholder/gradle-one-jar
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: "io.dropwizard", name: "dropwizard-core", version: "${dropwizardVersion}"
    compile group: "io.dropwizard", name: "dropwizard-jdbi", version: "${dropwizardVersion}"
    compile group: "io.dropwizard", name: "dropwizard-migrations", version: "${dropwizardVersion}"
    compile group: "io.dropwizard", name: "dropwizard-auth", version: "${dropwizardVersion}"

    compile group: "com.hubspot.dropwizard", name: "dropwizard-guice", version: "1.0.6.0"
    compile group: 'io.paradoxical', name: 'dropwizard-swagger', version: '3.0'
    compile group: "com.google.inject", name: "guice", version: "4.1.0"
    compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'

    testCompile group: "junit", name: "junit", version: "4.12"
}

// Configure the oneJar task
task oneJar(type: OneJar) {
    mainClass = mainClassName
}

task dbMigrate(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = mainClassName
    args 'db', 'migrate', './config.yml'
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', './config.yml'
}
